uses Fpl.Commons

def class Set

// "in relation" ("is element of") relation
def pred In(x,y: Set) infix "in" 5 
{
    intr
}

def pred IsEmpty(x: Set)
{
    all y:Set
    {
        not In(y, x) 
    }
}

// existence of an empty set
axiom EmptySetExists
{
    ex x:Set
    {
        IsEmpty(x)
    }
}

// introduction of a new mathematical object
def class EmptySet: Set
{
    ctor EmptySet()
    {
        dec 
            base.Set()
            assert IsEmpty(parent) 
        ;
        
    }
}

// relation between a subset and a superset
def pred IsSubset(subset,superset: Set)
{
    all u:Set
    {
        impl (In(u, subset), In(u, superset))
    }
}

// introduction of a new mathematical object that is a subset of a superset
def class Subset: Set
{
    ctor Subset(superSet: Set)
    {
        dec
            base.Set()
            assert IsSubset(parent, superSet)
        ;
        
    }
}

// extensionality
axiom Extensionality
{
    all x,y:Set
    {
        impl
        (
            and
            (
                IsSubset(x,y),
                IsSubset(y,x)
            ),
            ( x = y )
        )
    }
}

// adds Roster notation capability to this theory
def class SetRoster: Set
{
    // to support enumerating set elements, e.g. SetRoster(1,2,3)
    ctor SetRoster(listOfSets:* Set[ind])
    {
        dec 
            ~elem: Set
            base.Set()
            for elem in listOfSets
            {
                assert In(elem, parent)
            }
        ;
        
    }

}

// adds set-builder notation capability to this theory
// p is a predicate, in which the variable u is bound and the type of u is a Set
// moreover, p can accept even more bound variables of any type.
def class SetBuilder: Set
{
    // to support set builder notation, ex. all "even" natural numbers like SetBuilder(n: Nat, Even(n))
    ctor SetBuilder(x: Set, p: pred(u1: Set, o:* obj[ind]))
    {
        dec
        base.Set()
        assert
            all u2:Set
            {
                iif
                (
                    In(u2,parent),
                    and ( In(u2,x), p(u2,o) )
                )
            }
        ;
        
    }
}

// Schema of separation axioms
axiom SchemaSeparation
{
    all p:pred, x:Set
    {
        ex y:Set
        {
            ( y = SetBuilder(x,p) )
        }
    }
}



// For any two sets $x,y$ there exists a set $z$ containing them as elements.
axiom Pairing
{
    all x,y:Set
    {
        ex z:Set
        {
            all w:Set
            {
                iif
                (
                    In(w,z),
                    or
                    (
                        ( w = x ),
                        ( w = y )
                    )
                )
            }
        }
    }
}

// for every set x there is a set containing all elements of the elements of x
axiom Union
{
    all x:Set
    {
        ex y:Set
        {
            all z,w:Set
            {
                impl
                (
                    and (In(z,w), In(w,x)),
                    In(z,y)
                )
            }
        }
    }
}

def pred IsPowerSet(ofSet, potentialPowerSet: Set)
{
    all z:Set
    {
        impl (Subset(z,ofSet), In(z, potentialPowerSet))
    }
}

// including the PowerSet function
def func PowerSet(x: Set) -> Set
{
    dec 
        ~y: Set
        assert IsPowerSet(x,y)
    ;
    return y
}

// for every set x there is a set y containing all subsets of x as its elements
axiom PowerSetExistsForAllSets
{
    all x:Set
    {
        ex y:Set
        {
            IsPowerSet(x, y)
        }
    }
}

// introducing a set union function
def func SetUnion(x,y: Set) -> Set
{
    dec 
        ~union: Set
        assert
            all z:Set
            {
                iif
                (
                    or (In(z,x), In(z,y)),
                    In(z,union)
                )
            }
    ;
    return union
}

// introducing a singleton function
def func Singleton(x: Set) -> Set
{
    return SetRoster(x)
}

// alternative 2: adds SetUnion notation capability to this theory (as predicate)
def pred Union(x,superSet: Set)
{
    all u:Set
    {
        impl (In(u, x), In(u, superSet))
    }
}

;