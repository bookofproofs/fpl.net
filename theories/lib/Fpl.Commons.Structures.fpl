uses Fpl.Commons 
uses Fpl.SetTheory 
uses Fpl.PeanoArithmetics


// defines a fixed length tuple template using a generic type tplIndex for an index in a range
definition class Tuple : obj
{
    dec 
        ~myFrom:Nat
        ~myLength: Nat
        ~myArr: tpl[from, length:Nat]
    ;
    ctor Tuple(listOfTpl:+ tpl)
    {
        dec 
            ~elem: tpl
            ~i: Nat 
            i:= Zero() 
            for elem in listOfTpl
            {
                i:=Succ(i)
                myArr[i]:=elem
            }
            myLength:=i
            base.obj()
        ;
        self
    }

    ctor Tuple(arr: tpl[from, length:Nat])
    {
        
        dec 
            myArr:=arr
            myLength:=length
            myFrom:=from
            base.obj()
        ;
        self
    }

    property func Length() -> Nat
    {
        return myLength
    }

}

// defines a tuple template starting from a specific index with an arbitrary length
definition class UnlimitedTupleFrom: obj
{
    dec 
        ~myX:+ tpl
        ~myFrom:index
    ; 

    constructor UnlimitedTupleFrom(from:index, x:+ tpl)
    {
        dec
            base.obj()
            myX:=x
            myFrom:=from
        ;
        self
    }

    property func Coord(i: index) -> tpl
    {
        return myX[i]
    }
}

def class CartesianProduct: Set
{

    dec
        ~myLength: Nat
        ~myFrom: Nat
        ~mySetArray: Set[from , to:Nat]
    ;

    constructor CartesianProduct(setList:+ Set)
    {
        dec
            ~setItem: Set
            ~i: Nat
            base.Set()
            i:= Zero()
            for setItem in setList
            {
                i:=Succ(i)
                mySetArray[i]:=setItem
            }
            myFrom := Nat(@1)
            myLength:=i
        ;
        self
    }

    ctor CartesianProduct(setArray: Set[from , to:Nat])
    {
        dec
            mySetArray:=setArray
            myFrom:=from
            myLength:=to
            base.Set()
        ;
        self
    }

    property func Length() -> Nat
    {
        return myLength 
    }

    property pred AllTuplesIn()
    {
        and 
        (
            is(parent,Set),
            all tupleElem: Set
            {
                impl 
                (
                    In(tupleElem,parent),
                    and (
                        is(tupleElem,Tuple(a:Nat[x,y:ind])),
                        all i:Nat 
                        {
                            and (
                                In(i,Rang(@1,myLength)),    
                                In(tupleElem[i],parent[i]) 
                            )
                            
                        }
                    )
                )
            }
        )
    }

}

def class Relation: Set
{
    dec 
        ~myArity: Nat
        ~mySubset: Subset
    ;

    ctor Relation(setList:+ Set)
    {
        dec
            ~cartProd : CartesianProduct
            base.Set()
            cartProd := CartesianProduct(setList)
            myArity := cartProd.Length()
            mySubset := Subset(cartProd)
        ;
        self
    }

    property func Arity() -> Nat
    {
        return myArity
    }
}

def class BinaryRelation: Relation
{
    dec 
        ~myDomain, myCodomain: Set
    ;


    ctor BinaryRelation(x,y: Set)
    {
        dec
            base.Relation(@2,x,y)
            myDomain:=x
            myCodomain:=y
        ;
        self
    }

    property func Domain() -> Set 
    {
        return myDomain
    }

    property func CoDomain() -> Set
    {
        ret myCodomain
    }

    property pred RightUnique()
    {
        all v,w1,w2 : Set
        {
            impl
            (
                (
                    In(v,Domain()) and
                    In(w1,CoDomain()) and
                    In(w2,CoDomain()) and
                    AreRelated(v,w1,parent) and
                    AreRelated(v,w2,parent)
                ),
                ( w1 = w2 )
            )
        }
    }

    property pred LeftUnique()
    {
        all v1,v2,w : Set
        {
            impl
            (
                (
                    In(v1,Domain()) and
                    In(v2,Domain()) and
                    In(w,CoDomain()) and
                    AreRelated(v1,w,parent) and
                    AreRelated(v2,w,parent)
                ),
                ( v1 = v2 )
            )
        }
    }

    property pred Injective()
    {
        LeftUnique()
    }

    property pred Unique()
    {
        and (LeftUnique(), RightUnique())
    }

    property pred RightTotal()
    {
        all w : Set
        {
            ex v : Set
            {
                (
                    In(v,Domain()) and
                    In(w,CoDomain()) and
                    AreRelated(v,w,parent)
                )
            }
        }
    }

    property pred Surjective()
    {
        RightTotal()
    }

    property pred LeftTotal()
    {
        all v : Set
        {
            ex w : Set
            {
                (
                    In(v,Domain()) and
                    In(w,CoDomain()) and
                    AreRelated(v,w,parent)
                )
            }
        }
    }

    property pred Total()
    {
        and (LeftTotal(), RightTotal())
    }
}

def pred AreRelated(u,v: Set, r: BinaryRelation)
{
    dec
        ~tuple: Tuple

        tuple:=Tuple($1,$2)
    ;
    (
        In(tuple,r) and
        In(u,r.Domain()) and
        In(v,r.Codomain())
    )
}

definition class Function: BinaryRelation
{
    ctor Function(x,y: Set)
    {
        dec
            base.BinaryRelation(x,y)
            assert self.LeftTotal()
            assert self.RightUnique()
        ;
        self
    }
}

definition class EquivalenceRelation: BinaryRelation
{
    ctor EquivalenceRelation(x,y: Set)
    {
        dec
            base.BinaryRelation(x,y)
            assert self.Reflexive()
            assert self.Symmetric()
            assert self.Transitive()
        ;
        self
    }
}

;